# IF_syndep_100.ode

# From "Mechanism for the Universal Pattern of Activity in Developing Neuronal Networks", by
# Tabak, Mascagni, and Bertram. J Neurophysiol, 103:2208-2221, 2010.

# network of 100 I&F cells with heterogeneous input current (Ii)
# all to all coupling with synaptic depression
#

# cellular parameters
p i0=0.15 deli=1
p trefrac=.25 dtr=.05 vreset=0

# synaptic parameters
p taus=1 tauf=.1
p gsyn=2.8 vsyn=5
p alphad=0.004 betad=0.4

# create the table of applied currents Ii
 #uniformly spaced Ii in the interval [i0, i0+deli] :
table iapp % 100 0 99 i0+t*deli/99
 #random table of Ii distributed in the interval [i0, i0+deli] :
#table iapp % 100 0 99 ran(1)*(deli)+i0
 #or read a predefined table of values
#table table_name.tab

# differential equations
# v[i] membrane potential of cell i
# a[i] synaptic drive from cell i
# s[i] synaptic recovery for terminals from cell i
# tr[i] time of last spike from cell i
v[0..99]'=(-v[j]-gsyn*(atot-a[j]*s[j]/100)*(v[j]-vsyn)+iapp([j]))*heav(t-(tr[j]+trefrac))
tr[0..99]'=0
a[0..99]'=heav((tr[j]+dtr)-t)*(1-a[j])/tauf-a[j]/taus
s[0..99]'=alphad*(1-s[j])-betad*heav((tr[j]+dtr)-t)*s[j]

# synaptic drive to all cells
atot=sum(0,99)of(shift(s0,i')*shift(a0,i'))/100

# auxiliary variables for output
aux ave=sum(0,99)of(shift(a0,i'))/100
aux stot=sum(0,99)of(shift(s0,i'))/100

# only output these variables when running in silent mode
only t ave stot

# initial conditions
init v[0..99]=0 tr[j]=-5 s[j]=0.5 a[j]=0.01

 #reset of membrane potential after crossing firing threshold
global 1 {v[0..99]-1} {v[j]=vreset;tr[j]=t}

# simulation options
@ total=2000, bounds=100000 back=white maxstor=2000000 yp=ave
@ dt=0.001 njmp=100 xlo=0 xhi=2000 ylo=0 yhi=0.6 nplot=2 yp2=stot
done
