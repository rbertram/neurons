# E18_bif.ode

# This computer program was used in the article "Intrinsic Physiology
# of Inhibitory Neurons Changes Over Auditory Development" by
# Briana J. Carroll, Richard Bertram, and Richard L. Hyson,
# J. Neurophysiology, 119:290-304, 2018. It is a model for neurons
# at embryonic day 18 and was used to construct a bifurcation diagram
# for the neuron.

par from=100pip
par to=400
par ip=0

par C=42
 
# Nernst potentials (mV)
par VL=-40.6
par VNa=158.1
par VK=-97	

# Conductances	
par gL=4.6
par gNa=53.6
par gLT=6.8		
par gK=2.8			

# Time constants (mS)
par taunbar=10
par tauh=5

# Function constants in mV
par thetam=-35
par sigmam=-5
par thetan=-30
par sigman=-5
par thetah=-50
par sigmah=7
par thetay=-48
par sigmay=6
par thetaz=-71
par sigmaz=10

#
#Leak Current
iL=gL*(V-VL)

#Sodium Current (Na+)
minf=1/(1+exp((V-thetam)/sigmam))
hinf=1/(1+ exp((V-thetah)/sigmah))
iNa=gNa*(minf^3)*h*(V-VNa) 

#Potassium Current (K+)
ninf=1/(1+ exp((V-thetan)/sigman))
taun=taunbar/cosh((V-thetan)/(2*sigman)) 
iK=gK*(n^4)*(V-VK) 

# KLT (low-threshold activated potassium channel)
zinf = (1-.5)*(1/(1+exp((v-thetaz)/sigmaz))) + .5
yinf = (1+exp(-(v-thetay)/sigmay))^(-.25)
tauz = 1000/(exp((v+60)/20) + exp(-(v+60)/8)) + 50
tauy = 100/(6*exp((v+60)/6) + 16*exp(-(v+60)/45)) + 1.5
iLT = gLT*y^4*z*(v-vk)


# Differential Equations
V'=(-iL-iK-iNa-iLT+ip)/C
y' = (yinf-y)/tauy
z' = (zinf-z)/tauz
n'=(ninf-n)/(taun)
h'=(hinf-h)/(tauh)

#init 
V(0)=-52.51999
y(0)=0.7521768
z(0)=0.5680539
n(0)=0.01094357
h(0)=0.5890402

aux iNa = iNa
aux iK = iK
aux iL = iL
aux ip = ip
@ dt=0.01, total=500, maxstor=2000000

@ bounds=10000000, xp=t, yp=V

@ xlo=0, xhi=500, ylo=-120, yhi=60

done
